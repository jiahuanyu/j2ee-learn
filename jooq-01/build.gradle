buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath("nu.studer:gradle-jooq-plugin:3.0.3")
    }
}

dependencies {
    compileOnly("javax.servlet:servlet-api:2.5")
    compile("org.jooq:jooq:3.11.2")
    compile("org.jooq:jooq-codegen:3.11.2")
    compile("org.jooq:jooq-meta:3.11.2")
    compile("javax.validation:validation-api:2.0.1.Final")
    compile("mysql:mysql-connector-java:8.0.12")
}

apply plugin: 'nu.studer.jooq'

jooq {
    dependencies {
        jooqRuntime("mysql:mysql-connector-java:8.0.12")
    }

    j2eeLearn (sourceSets.main){
        def schema = 'j2ee-learn'

        jdbc {
            // JDBC驱动
            driver = 'com.mysql.jdbc.Driver'
            // 数据库连接url
            url = 'jdbc:mysql://localhost:3306/j2ee-learn?useUnicode=true&characterEncoding=UTF-8&rewriteBatchedStatements=true'
            // 数据库用户名
            user = 'root'
            // 数据库密码
            password = 'root1234'
        }

        generator {
            // 生成器，可自定义
            // org.jooq.codegen.JavaGenerator
            // org.jooq.codegen.ScalaGenerator
            // 默认是 JavaGenerator
            name = 'org.jooq.codegen.DefaultGenerator'

//            strategy {
//                name = 'me.jiahuan.java.strategy.JooqGeneratorStrategy'
//            }

            database {
                // 数据库类型
                // org.jooq.meta.[database].[database]Database
                name = 'org.jooq.meta.mysql.MySQLDatabase'
                // 指定数据库名
                inputSchema = 'j2ee-learn'
                // 包含表名
                includes = '.*'
                // 不包含表名
                excludes = 'schema_version'

                customTypes {
                    customType {
                        name = 'Gender'
                        type = 'me.jiahuan.java.enums.Gender'
                        converter = 'me.jiahuan.java.converter.GenderEnumConverter'
                    }
                }

                forcedTypes {
                    forcedType {
                        name = 'Gender'
                        expression = 'gender'
                        types = '.*'
                    }
                }
            }

            generate {
                pojos = true
                pojosEqualsAndHashCode = true
                fluentSetters = true
                validationAnnotations = true
                springAnnotations = true
            }

            target {
                packageName = 'me.jiahuan.java.model'
                directory = "$projectDir/gensrc/main/java"
            }
        }
    }
}
